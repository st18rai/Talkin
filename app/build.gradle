buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

repositories {
    //Crashlytics
    maven {
        url 'https://maven.fabric.io/public'
    }
    //Mockito
    jcenter()
}

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.internship.droidz.talkin"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        jackOptions {
            enabled true
        }
        multiDexEnabled true
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + "(" + defaultConfig.versionCode  + ").apk"))
        }
    }

    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {

        def INT = "int"
        def BOOLEAN = "boolean"
        def STRING = "String"
        def TRUE = "true"
        def FALSE = "false"

        def REPORT_CRASHES = "REPORT_CRASHES"

        debug {
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE

            signingConfig signingConfigs.debug
        }

        debugDev {
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE

            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    // xml serialize\deserialize
    compile('org.simpleframework:simple-xml:2.7.+') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    //Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'

    //phone mask
    compile 'ru.tinkoff.decoro:decoro:1.1.1'
    //circle view
    compile 'de.hdodenhof:circleimageview:2.1.0'
    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-simplexml:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    //RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.5'
    //rx bindings
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    //Gson
    compile 'com.google.code.gson:gson:2.4'
    // okhttp
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    //Quickblox
    compile 'com.quickblox:quickblox-android-sdk-chat:3.3.0'
    compile 'com.android.support:multidex:1.0.1'
    //Firebase
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    //Multidex
    compile 'com.android.support:multidex:1.0.1'
    //Facebook
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
    // Moxy
    compile 'com.arello-mobile:moxy:1.4.5'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.4.5'
    compile 'com.arello-mobile:moxy-app-compat:1.4.5'
    //Mockito
    testCompile 'org.mockito:mockito-core:2.0.57-beta'
    //JUnit
    testCompile 'junit:junit:4.12'
    // Leakcanary
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    // SwipeLayout
    compile "com.daimajia.swipelayout:library:1.2.0@aar"

}

apply plugin: 'com.google.gms.google-services'


